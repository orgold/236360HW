%{
	#include <iostream>
	#include <stdlib.h>
	#include <cstring>
	#include "hw4_table_utils.h"
	#include "semantic_utils.h"
	using namespace std;

	extern int yylex();
	void yyerror(char * message);
	SymbolTable symbolTable;
	RegPool regPool;
	whileStack while_stack;
	TYPE globalFunctionRetType;
	bool globalIsDeclSensitive = 0;
	vector<int> returnAdrressesBapatch(0);
	extern int yylineno;
%}

%locations

%left OR;
%left AND;
%left RELOP2;
%left RELOP1;
%left BINOP2;
%left BINOP1;
%right NOT;
%token SC;
%token COLON;
%token ASSIGN;
%token RETURN;
%token BREAK;
%token LBRACE;
%token RBRACE;
%token IF;
%token ELSE;
%token WHILE;
%token SWITCH;
%token CASE;
%nonassoc LPAREN;
%nonassoc RPAREN;
%nonassoc COMMA;
%nonassoc VOID;
%nonassoc INT;
%nonassoc BYTE;
%nonassoc BOOL;
%nonassoc TRUE FALSE;
%nonassoc ID;

%nonassoc STRING;
%nonassoc NUM;
%nonassoc B;

%%


program 
	: funcs 		{
						
					}


funcs 
	: fundecl funcs			{}
	| 						{/*cout<< "took the empty" << endl;*/}


statements
	: statement				{
								$$.hasBreak = $1.hasBreak;
								$$.lineno = $1.lineno;
								//cout<<"statements -> statement"<<endl;
							}
	| statements statement 	{
								if($1.hasBreak){
										$$.hasBreak = 1;
										$$.lineno = $1.lineno;
									}
									else {
										$$.hasBreak = $2.hasBreak;
										$$.lineno = $2.lineno;
									}
								//cout<<"statements -> statements statement"<<endl;
							}

statement
	: openstatement			{
								$$.hasBreak = $1.hasBreak;
								$$.lineno = $1.lineno;
								//cout<<"statement -> openstatement"<<endl;
							}
	| closedstatement		{
								$$.hasBreak = $1.hasBreak;
								$$.lineno = $1.lineno;
								//cout<<"statement -> closedstatement"<<endl;
							}

MarkOn:  {globalIsDeclSensitive = true;}
MarkOff: {globalIsDeclSensitive = false;}

IfStart: IF LPAREN exp 			{
								if($3.type != BOOL_T)
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.trueList = $3.trueList;
									$$.falseList = $3.falseList;
								}

WhileStart : WHILE M LPAREN exp {

									if($4.type != BOOL_T)
									{
										errorMismatch($1.lineno);
										exit(1);
									}

									while_stack.addVector();
									$$.trueList = $4.trueList;
									$$.falseList = $4.falseList;
									$$.nextInst = $2.nextInst;
								}

openstatement
	: IfStart RPAREN MarkOn M statement MarkOff			{
																
															CodeBuffer::instance().bpatch(*($1.trueList),$4.nextInst);
															delete $1.trueList;
															free($4.nextInst);
															string nextLabel = CodeBuffer::instance().next();
															CodeBuffer::instance().bpatch(*($1.falseList),nextLabel);
															delete $1.falseList;
															$$.hasBreak = $5.hasBreak;
															$$.lineno = $5.lineno;
															//cout<<"openstatement -> IF LPAREN exp RPAREN statement"<<endl;
														}

	| IfStart RPAREN MarkOn M closedstatement MarkOff ELSE MarkOn N M  openstatement MarkOff	
		{
			
			CodeBuffer::instance().bpatch(*($1.trueList),$4.nextInst);
			delete $1.trueList;
			free($4.nextInst);
			CodeBuffer::instance().bpatch(*($1.falseList),$9.nextInst);
			delete $1.falseList;
			free($9.nextInst);

			string nextLabel = CodeBuffer::instance().next();
			CodeBuffer::instance().bpatch(CodeBuffer::makelist($8.asmLineNo),nextLabel);

			if($5.hasBreak)
				{
					$$.hasBreak=1;
					$$.lineno = $5.lineno;
				}
				else 
				{
					$$.hasBreak=$11.hasBreak;
					$$.lineno = $11.lineno;
				}
			//cout<<"openstatement -> IF LPAREN exp RPAREN closedstatement ELSE openstatement"<<endl;
		}
	| WhileStart RPAREN MarkOn M openstatement MarkOff N M 		{
																	$$.hasBreak = 0;
																	CodeBuffer::instance().bpatch(*($1.trueList),$4.nextInst);
																	delete $1.trueList;
																	free($4.nextInst);

																	CodeBuffer::instance().bpatch(*($1.falseList),$8.nextInst);
																	delete $1.falseList;
																	while_stack.removeVector($8.nextInst);
																	CodeBuffer::instance().bpatch(CodeBuffer::makelist($7.asmLineNo),$1.nextInst);
																	free($1.nextInst);
																	free($8.nextInst);
																	//cout<<"openstatement -> WHILE LPAREN exp RPAREN openstatement"<<endl;
																}

closedstatement
	: simplestatement 											{
																	$$.hasBreak = $1.hasBreak;
																	$$.lineno = $1.lineno;
																	//cout<<"closedstatement -> simplestatement"<<endl;
																}
	| IfStart RPAREN MarkOn M closedstatement MarkOff ELSE MarkOn N M closedstatement MarkOff
		{
			CodeBuffer::instance().bpatch(*($1.trueList),$4.nextInst);
			delete $1.trueList;
			free($4.nextInst);
			CodeBuffer::instance().bpatch(*($1.falseList),$10.nextInst);
			delete $1.falseList;
			free($10.nextInst);
			string nextLabel = CodeBuffer::instance().next();
			CodeBuffer::instance().bpatch(CodeBuffer::makelist($9.asmLineNo),nextLabel);

			if($5.hasBreak)
			{
				$$.hasBreak=1;
				$$.lineno = $5.lineno;
			}
			else 
			{
				$$.hasBreak=$11.hasBreak;
				$$.lineno = $11.lineno;
			}
			//cout<<"closedstatement -> IF LPAREN exp RPAREN closedstatement ELSE closedstatement"<<endl;
		}
	| WhileStart RPAREN MarkOn M closedstatement MarkOff N M	{
																	CodeBuffer::instance().bpatch(*($1.trueList),$4.nextInst);
																	delete $1.trueList;
																	free($4.nextInst);
																	CodeBuffer::instance().bpatch(*($1.falseList),$8.nextInst);
																	delete $1.falseList;
																	while_stack.removeVector($8.nextInst);
																	CodeBuffer::instance().bpatch(CodeBuffer::makelist($7.asmLineNo),$1.nextInst);
																	free($1.nextInst);
																	free($8.nextInst);
																	$$.hasBreak = 0;
																	//cout<<"closedstatement -> WHILE LPAREN exp RPAREN closedstatement"<<endl;
																}
	| SWITCH LPAREN exp {if($3.type != BYTE_T && $3.type != INT_T){errorMismatch($1.lineno);exit(1);}} N RPAREN LBRACE caselist RBRACE M SC		
	{
		CodeBuffer::instance().bpatch(CodeBuffer::makelist($5.asmLineNo),$10.nextInst);
		makeSwitchCode($3.place, *($8.caseStack), *($8.nextList));
		//cleanup:
		regPool.freeReg($3.place);//TODO consider moving into marker, should not cause an issue.
		delete $8.caseStack;
		delete $8.nextList;
		free($10.nextInst);

		//handle break for while:
		$$.hasBreak = $8.hasBreak;
		$$.lineno = $8.lineno;
		//cout<<"closedstatement -> SWITCH LPAREN exp RPAREN LBRACE caselist RBRACE SC"<<endl;
	}

caselist
	: casestat					{
									$$.caseStack = $1.caseStack;
									$$.nextList = $1.nextList;
									if($1.hasBreak){
										$$.hasBreak = 1;
										$$.lineno = $1.lineno;
									}
									//cout<<"caselist -> casestat"<<endl;
								}
	| casestat caselist			{
									//handle caseStack
									$2.caseStack->push_back((*$1.caseStack)[0]);
									delete $1.caseStack;
									$$.caseStack = $2.caseStack;
									$2.nextList->push_back((*$1.nextList)[0]);
									delete $1.nextList;
									$$.nextList = $2.nextList;

									//handle break for while
									if($1.hasBreak){
										$$.hasBreak = 1;
										$$.lineno = $1.lineno;
									}
									else {
										$$.hasBreak = $2.hasBreak;
										$$.lineno = $2.lineno;
									}
									//cout<<"caselist -> casestat caselist"<<endl;
								}

casestat
	: CASE NUM COLON   MarkOn M statement MarkOff BREAK N SC 		
	{
		$$.caseStack = new vector<pair<int,string> >;
		$$.caseStack->push_back(make_pair(atoi($2.value),$5.nextInst));
		$$.nextList = new vector<int>;
		$$.nextList->push_back($9.asmLineNo);
		
		$$.hasBreak = $6.hasBreak;
		$$.lineno = $6.lineno;

		//cleanup:
		free($2.value);
		free($5.nextInst);
		//cout<<"casestat -> CASE NUM COLON statement BREAK SC"<<endl;
	}
	| CASE NUM B COLON MarkOn M statement MarkOff BREAK N SC 
	{
		$$.caseStack = new vector<pair<int,string> >;
		$$.caseStack->push_back(make_pair(atoi($2.value),$6.nextInst));
		$$.nextList = new vector<int>;
		$$.nextList->push_back($10.asmLineNo);

		$$.hasBreak = $7.hasBreak;
		$$.lineno = $7.lineno;

		//cleanup:
		free($2.value);
		free($6.nextInst);
		//cout<<"casestat -> CASE NUM B COLON statement BREAK SC"<<endl;
	}

simplestatement 
	: type ID SC				{
									if(globalIsDeclSensitive)
										symbolTable.insertScope();
									try{
										symbolTable.insertVar($2.idName,$1.type);
									}
									catch(errorDefException e)
									{
										errorDef($2.lineno,e.id.c_str());
										exit(1);
									}
									if(globalIsDeclSensitive)
										symbolTable.removeScope();
									free($2.idName);
									//$$.lineno = $2.lineno;
									$$.hasBreak =0;
									//cout<<"simplestatement -> type ID SC"<<endl;
								}
	| type ID ASSIGN exp SC 	{
									if( $1.type!= $4.type &&
									  !($1.type==INT_T&&$4.type==BYTE_T)) 
									{
										errorMismatch($2.lineno);
										exit(1);
									}
									REG valPlace = $4.place;
									if($1.type == BOOL_T && NULL != $4.trueList)
										valPlace = putSetRegCode($4.trueList,$4.falseList);
									if(globalIsDeclSensitive)
										symbolTable.insertScope();
									try{
										symbolTable.insertVar($2.idName,$1.type);
									}
									catch(errorDefException e)
									{
										errorDef($2.lineno,e.id.c_str());
										exit(1);
									}
									moveValueInVar($2.idName, valPlace);
									//done
									if(globalIsDeclSensitive)
										symbolTable.removeScope();
									
									free($2.idName);
									$$.lineno = $2.lineno;
									$$.hasBreak =0;
									//cout<<"simplestatement -> type ID ASSIGN exp SC"<<endl;
								}
	| ID ASSIGN exp SC 			{
									try{
										TYPE tempType=symbolTable.getType($1.idName);
										if(tempType!= $3.type && !(tempType==INT_T&&$3.type==BYTE_T)) 
										{
											errorMismatch($2.lineno);
											exit(1);
										}
										REG valPlace = $3.place;
										if(tempType == BOOL_T && NULL != $3.trueList)
											valPlace = putSetRegCode($3.trueList,$3.falseList);
										moveValueInVar($1.idName, valPlace);
									}
									catch(errorUndefException& e)
									{
										errorUndef($1.lineno,$1.idName);
										exit(1);
									}
									
									free($1.idName);
									$$.hasBreak =0;
									//cout<<"simplestatement -> ID ASSIGN exp SC"<<endl;
								}
	| call SC 					{
									$$.hasBreak =0;
									//cout<<"simplestatement -> call SC"<<endl;
								}
	| RETURN SC 				{
									if(globalFunctionRetType != VOID_T) 
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.hasBreak =0;
									//cout<<"simplestatement -> RETURN SC"<<endl;
									int address = CodeBuffer::instance().emit("b ");
									returnAdrressesBapatch.push_back(address) ; 

								}
	| RETURN exp SC				{
									if( globalFunctionRetType != $2.type && 
									    !(globalFunctionRetType ==INT_T &&$2.type == BYTE_T )
										|| globalFunctionRetType == VOID_T)
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									
									$$.hasBreak =0;
									
									CodeBuffer::instance().emit("move $v0 ,"+RegPool::regToString($2.place) );
									regPool.freeReg($2.place);
									int address = CodeBuffer::instance().emit("b ");
									returnAdrressesBapatch.push_back(address) ; 
								}
	| BREAK SC 					{	
									$$.hasBreak = true;
									$$.lineno = $1.lineno;
									while_stack.addAddress();
									//cout<<"simplestatement -> BREAK SC"<<endl;
								}
	| LBRACE {symbolTable.insertScope(); globalIsDeclSensitive=false;} statements RBRACE 	
		{						
			$$.hasBreak = $2.hasBreak;
			$$.lineno = $2.lineno;
			symbolTable.removeScope();
			//cout<<"simplestatement -> { statements }"<<endl;
		}

	

fundecl 
	: fundecl_helper RPAREN LBRACE statements M RBRACE 		{
																if($4.hasBreak)
																{
																	errorUnexpectedBreak($4.lineno);
																	exit(1);
																}
																CodeBuffer::instance().bpatch(returnAdrressesBapatch,$5.nextInst);
																free($5.nextInst);
																returnAdrressesBapatch.clear();
																symbolTable.functionRemoveScope($1.typeListSize);
																CodeBuffer::instance().emit("jr $ra");
																//cout<<"fundecl -> rettype ID LPAREN formals RPAREN"<<endl;
															}

fundecl_helper 
	: rettype ID LPAREN formals   	{
										CodeBuffer::instance().emit(string($2.idName) + ":");
										initFP();
										setRaOnStack($4.typeListSize);
										$$.typeListSize = $4.typeListSize;
										try {
											symbolTable.coverInsertFunction($2.idName,$4.typeList,$4.typeListSize,$1.type,$4.idList);
										}
										catch(errorDefException& e)
										{
											errorDef($2.lineno,e.id.c_str());
											exit(1);
										}
										globalFunctionRetType = $1.type;
										$$.type=$1.type;
										free($2.idName);
										free($4.typeList);
										for(int i = 0; i < $4.typeListSize ; i++)
											free($4.idList[i]);
										free($4.idList);
									}
exp 
	: exp BINOP1 exp 			{
									if( $1.type != INT_T  &&
										$1.type != BYTE_T ||
										$3.type != INT_T  &&
										$3.type != BYTE_T )
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									if( $1.type == INT_T ||
										$3.type == INT_T )
									{
										$$.type = INT_T;
									} else {
										$$.type = BYTE_T;
									}
									$$.place = expMakeBinOp1($1.place, $3.place, $2.value);
									$$.lineno = $1.lineno;
									//cout<<"exp -> exp BINOP1 exp"<<endl;
								}
	| exp BINOP2 exp			{
									if( $1.type != INT_T  &&
										$1.type != BYTE_T ||
										$3.type != INT_T  &&
										$3.type != BYTE_T )
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									if( $1.type == INT_T ||
										$3.type == INT_T )
									{
										$$.type = INT_T;
									} else {
										$$.type = BYTE_T;
									}
									$$.place = expMakeBinOp2($1.place,$3.place,$2.value);
									$$.lineno = $1.lineno;
									
									//cout<<"exp -> exp BINOP2 exp: $$ type type is: " << toCString($$.type)<<endl;
								}
	| NUM 						{
									$$.type = INT_T;
									$$.lineno = $1.lineno;
									$$.place = expInit ($1.value);
									free($1.value);
									//cout<<"exp -> num"<<endl;
								}
	| LPAREN exp RPAREN 		{
									$$.type = $2.type;
									$$.place = $2.place;
									//if boolean, need to keep track of lists
									$$.trueList  = $2.trueList;
									$$.falseList = $2.falseList;
									//cout<<"exp -> LPAREN exp RPAREN"<<endl;
								}
	| NOT exp					{
									if($2.type != BOOL_T){
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.type = BOOL_T;
									$$.lineno = $1.lineno;
									//insert to asm
									$$.trueList  = $2.falseList;
									$$.falseList = $2.trueList;
									//cout<<"exp -> NOT exp"<<endl;
								}
	| exp AND M exp 			{
									if( $1.type != BOOL_T ||
										$4.type != BOOL_T)
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.type = BOOL_T;
									$$.lineno = $1.lineno;
									//insert asm
									CodeBuffer::instance().bpatch(*($1.trueList),$3.nextInst);
									delete $1.trueList;
									vector<int> listMerge = CodeBuffer::merge(*$1.falseList,*$4.falseList);
									$$.falseList = new vector<int>(listMerge.begin(),listMerge.end());
									delete $1.falseList;
									delete $4.falseList;
									$$.trueList = $4.trueList;
									//cout<<"exp -> exp AND exp"<<endl;
								}
	| exp OR M exp 				{
									if( $1.type != BOOL_T ||
										$4.type != BOOL_T)
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.type = BOOL_T;
									$$.lineno = $1.lineno;
									//insert asm
									CodeBuffer::instance().bpatch(*($1.falseList),$3.nextInst);
									delete $1.falseList;
									vector<int> listMerge = CodeBuffer::merge(*$1.trueList,*$4.trueList);
									$$.trueList = new vector<int>(listMerge.begin(),listMerge.end());
									delete $1.trueList;
									delete $4.trueList;
									$$.falseList = $4.falseList;
									//cout<<"exp -> exp OR exp"<<endl;
								}
	| TRUE						{
									$$.type=BOOL_T;
									$$.lineno = $1.lineno;
									//insert to asm
									int asmLineNo =  CodeBuffer::instance().emit("b ");
									vector<int> tmpTrueList = CodeBuffer::makelist(asmLineNo);
									$$.trueList = new vector<int>(tmpTrueList.begin(),tmpTrueList.end());
									$$.falseList = new vector<int>(0);;
									//cout<<"exp -> TRUE"<<endl;
								}
	| FALSE 					{
									$$.type=BOOL_T;
									$$.lineno = $1.lineno;
									//insert to asm
									int asmLineNo =  CodeBuffer::instance().emit("b ");
									vector<int> tmpList = CodeBuffer::makelist(asmLineNo);
									$$.falseList = new vector<int>(tmpList.begin(),tmpList.end());
									$$.trueList = new vector<int>(0);
									//cout<<"exp -> FALSE"<<endl;
								}
	| exp RELOP1 exp 			{
									if( $1.type != INT_T  &&
										$1.type != BYTE_T ||
										$3.type != INT_T  &&
										$3.type != BYTE_T )
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.type = BOOL_T;
									$$.lineno = $1.lineno;
									//insert asm
									expMakeRelOp2($1.place,$3.place,$2.value,&($$.trueList),&($$.falseList));
									//cout<<"exp -> exp RELOP1 exp"<<endl;
								}
	| exp RELOP2 exp 			{
									if( $1.type != INT_T  &&
										$1.type != BYTE_T ||
										$3.type != INT_T  &&
										$3.type != BYTE_T )
									{
										errorMismatch($1.lineno);
										exit(1);
									}
									$$.type = BOOL_T;
									$$.lineno = $1.lineno;
									//insert asm
									expMakeRelOp2($1.place,$3.place,$2.value,&($$.trueList),&($$.falseList));
									//cout<<"exp -> exp RELOP2 exp"<<endl;
								}
	| STRING 					{
									$$.type = STRING_T;
									$$.lineno = $1.lineno;
									//cout<<"exp -> STRING"<<endl;
								}
	| ID 						{
								try{
									$$.lineno = $1.lineno;
									$$.type=symbolTable.getType($1.idName);
								}
								catch (errorUndefException& e)
								{
									errorUndef($$.lineno,e.id.c_str()); 
									exit(1);
								}
								$$.place = loadValueFromVar($1.idName);
								$$.trueList = NULL;
								$$.falseList = NULL;
								//cout<<"exp -> ID: " << $1.idName << " of type: " << toCString($$.type) <<endl;
								free($1.idName);
								}
								
	| NUM B 					{
									if (atoi($1.value)>255){
										errorByteTooLarge($1.lineno,$1.value);//TODO change this
										exit(1);
									}
									$$.type = BYTE_T;
									$$.lineno = $1.lineno;
									$$.place = expInit ($1.value);
									free($1.value);
									//cout<<"exp -> NUM B"<<endl;
								}
	| call 						{
									$$.place = regPool.getReg();
									CodeBuffer::instance().emit("move "+ RegPool::regToString($$.place)+" , $v0");
									$$.type = $1.type;
									//cout<<"exp -> call"<<endl;
								}

call 
	: ID LPAREN RPAREN 			{
								symbolTable.functionCallCode();
								CodeBuffer::instance().emit("jal "+ string($1.idName));
								try {
								$$.type=symbolTable.checkFuncTypeAndArgs($1.idName,NULL,0);
								}
								catch (errorUndefFuncException& e){
									errorUndefFunc($1.lineno,e.id.c_str());
									exit(1);
								}
								catch (errorPrototypeMismatchException& e)
								{
									errorPrototypeMismatch($1.lineno,e.id.c_str(),e.argTypes);
									exit(1);
								}
								free($1.idName);
								
								$$.lineno = $1.lineno;

									//cout<<"exp -> ID LPAREN RPAREN"<<endl;
									}
	| ID LPAREN explist RPAREN 	{


								symbolTable.functionCallCode();
								pushArgsList(*($3.argListRegs));
								delete $3.argListRegs;
								CodeBuffer::instance().emit("jal "+ string($1.idName));
								try {
								$$.type=symbolTable.checkFuncTypeAndArgs($1.idName,$3.typeList,$3.typeListSize);
								}
								catch (errorUndefFuncException& e){
									errorUndefFunc($1.lineno,e.id.c_str());
									exit(1);
								}
								catch (errorPrototypeMismatchException& e)
								{
									errorPrototypeMismatch($1.lineno,e.id.c_str(),e.argTypes);
									exit(1);
								}
								$$.lineno = $1.lineno;
								free($1.idName);
								free($3.typeList);
								//cout<<"exp -> ID LPAREN explist RPAREN"<<endl;
								}

explist 
	: exp 						{
								$$.typeList=(TYPE*)malloc(sizeof(TYPE));
								$$.typeListSize=1;
								$$.typeList[0]=$1.type;
								$$.argListRegs = new vector<REG> (1,$1.place);

								//cout<<"explist -> exp"<<endl;
								}
	| exp COMMA explist  		{
								$$.typeList =(TYPE*) realloc($3.typeList,sizeof(TYPE)*($3.typeListSize+1));
								$$.typeListSize=$3.typeListSize+1;
								$$.typeList[$3.typeListSize]=$1.type;
								//cout<<"explist -> exp COMMA explist"<<endl;
								$$.argListRegs = $3.argListRegs;
								$$.argListRegs->push_back($1.place);
								}

type 
	: INT 						{
								$$.type=INT_T;
								//cout<<"type -> INT"<<endl;
								}
	| BYTE 						{
								$$.type=BYTE_T;
								//cout<<"type -> BYTE"<<endl;
								}
	| BOOL 						{
								$$.type=BOOL_T;
								//cout<<"type -> BOOL"<<endl;
								}

formals 
	: 							{
									$$.typeListSize = 0;
									//cout<<"formals -> eps"<<endl;
								}
	| formalslist 				{
									$$.typeList = $1.typeList;
									$$.typeListSize = $1.typeListSize;
									$$.idList = $1.idList;
									//cout<<"formals -> formalslist"<<endl;
								}

formalslist	
	: formaldecl 					{
										$$.typeList = (TYPE*) malloc(sizeof(TYPE));
										$$.typeListSize = 1;
										$$.typeList[0] = $1.type;
										$$.idList = (char**) malloc(sizeof(char*));
										$$.idList[0] = $1.idName;
										//cout<<"formalslist -> formaldecl"<<endl;
									}
	| formaldecl COMMA formalslist 	{
										$$.typeList = (TYPE*) realloc($3.typeList, sizeof(TYPE)*($3.typeListSize + 1));
										$$.typeListSize = $3.typeListSize + 1;
										$$.typeList[$3.typeListSize] = $1.type;
										$$.idList = (char**) realloc($3.idList, sizeof(char*)*($3.typeListSize + 1));
										$$.idList[$3.typeListSize] = $1.idName;
										//cout<<"formalslist -> formaldecl COMMA formalslist"<<endl;
									}

formaldecl : type ID 			{
									$$.type = $1.type;
									$$.idName = $2.idName;
									//cout<<"formaldecl -> type ID "<<endl;
								}

rettype 
	: type 						{
								$$.type=$1.type;
								//cout<<"rettype -> type  "<<endl;
								}
	| VOID 						{
								$$.type=VOID_T;
								//cout<<"rettype -> VOID"<<endl;
								}

M:							{
								string nextLabel = CodeBuffer::instance().next();
								$$.nextInst = strdup(nextLabel.c_str());
							}
N:							{
							$$.asmLineNo =  CodeBuffer::instance().emit("b ");
							}								
%%

int main()
{
	insert_printi();
	insert_print();
	insert_div0error();
	yyparse();
	try{
		TYPE typeTemp = symbolTable.checkFuncTypeAndArgs("main", NULL,0);
		if (typeTemp != VOID_T){
			errorMainMissing();
			exit(1);
		}
	}
	catch (errorUndefFuncException& e){
		errorMainMissing();
		exit(1);
	}
	catch (errorPrototypeMismatchException& e){
		errorMainMissing();
		exit(1);
	}
	symbolTable.removeGlobalScope();
	CodeBuffer::instance().printDataBuffer();
	CodeBuffer::instance().printCodeBuffer();
	
	//cout<<"program -> funcs"<<endl;
}

void yyerror(char* message)
{
	errorSyn(yylineno);
	exit(0);
}